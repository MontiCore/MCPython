/* (c) https://github.com/MontiCore/monticore */
package de.monticore;

/*
 * The following utf-8 symbols are used to parse the Python files without having to add whitespace to the Grammar.
 * In a preprocessing step code blocks are denoted with \u204f = ⦃ and \u2984 = ⦄, and lines are ended with \u204f = ⁏  
 */

grammar Python extends  de.monticore.MultilineString, // Must be first to avoid conflicts with String token!
                        de.monticore.expressions.CommonExpressions,
                        de.monticore.expressions.AssignmentExpressions,
                        de.monticore.literals.MCCommonLiterals,
                        de.monticore.symbols.OOSymbols {

  PythonScript = Statement*;

  @Override
  token SL_COMMENT = "#" (~('\n' | '\r' ))* : ->skip {storeComment();};
  token BLOCK_START = '\u2983';
  token BLOCK_END = '\u2984';
  token STATEMENT_END = '\u204f';

  // Will be filtered out by the WhitespacePreprocessingTokenSource
  // Used break a line without finishing the statement
  token CONTINUE_LINE_TOKEN = '\\' '\r'? '\n';

  // Often (mis)used as a multiline comment but can also be used as a string literal, this we can not skip it
  token MultiLineStringToken = ((("\'\'\'") .*? ("\'\'\'")) | ((DoubleQuoteMultilineStringDelimiter .*? DoubleQuoteMultilineStringDelimiter)));
  MultiLineStringLiteral implements Literal = MultiLineStringToken;

  interface Statement;

  // The StatementBlock represents a number of statements with the same indentation
  // Here, C-Style brackets are used instead of indentation
  scope StatementBlock = BLOCK_START StatementBlockBody BLOCK_END;
  StatementBlockBody = Statement+;

  /*===========================Statements======================================*/
  ElseStatementPart = "else" ":" StatementBlock;

  PyQualifiedName = (Name || ".")+;
  astrule PyQualifiedName = method public String joined(){
    return String.join(".", getNameList());
  };

  // import statement
  ImportStatement implements Statement = ( "from" (leadingDots:"."+)? module:PyQualifiedName )? "import" (star:"*" | name:(PyQualifiedName || ",")+) ( "as" alias:Name )? STATEMENT_END;

  // if-else statement
  IfStatement implements Statement = "if"   condition:Expression     ":" thenStatement:StatementBlock
                                   ("elif" elifCondition:Expression ":" elifStatement:StatementBlock )*
                                   ElseStatementPart?;

  // assert statement
  AssertStatement implements Statement = "assert" condition:Expression "," errorMessage:String STATEMENT_END;

  // for statement
  scope ForStatement implements Statement = "for" ForControl ":" StatementBlock
                                            ElseStatementPart? ;

  interface ForControl ;
  CommonForControl   implements ForControl =  ForVariable          "in" ForIterable ;
  UnpackedForControl implements ForControl = (ForVariable || ",")+ "in" ForIterable ;

  ForIterable = Expression;
  ForVariable implements Variable = Name;

  // while statement
  WhileStatement implements Statement = "while" condition:Expression ":" StatementBlock
                                      ElseStatementPart? ;

  BreakStatement implements Statement = "break" STATEMENT_END;
  ContinueStatement implements Statement = "continue" STATEMENT_END;

  // try-except-finally statement
  TryExceptStatement implements Statement = "try"                           ":" tryStatement:StatementBlock
                                          ExceptStatement+
                                                        ElseStatementPart ?
                                          ("finally"           ":" finallyStatement:StatementBlock )? ;

  ExceptStatement = ( "except" (Name? | "(" (Name || ",")+ ")") ("as" alias:Name)? ":" StatementBlock ) ;

  // with open file statement
  scope WithOpenFileStatement implements Statement = "with" (OpenFileExpression || ",")+ ":"
                                            StatementBlock ;

  OpenFileExpression = Expression "as" fileName:Name ;

  // variable declaration statement
  LocalVariableDeclarationStatement implements Statement = VariableDeclaration STATEMENT_END;

  VariableDeclaration implements Variable = Name "=" VariableInit ;

  GlobalVariableDeclaration implements Statement = "global" Name STATEMENT_END;

  MultiVariableDeclaration implements Statement = (Name || ",")+ "=" Expression STATEMENT_END;

  interface VariableInit ;

  SimpleInit implements VariableInit = Expression ;

  ArrayLiteralExpression  implements Expression = "[" (VariableInit || ",")* ","? "]" ;
  TupleLiteralExpression  implements Expression = "(" (VariableInit || ",")* ","? ")" ;
  DictLiteralExpression   implements Expression = "{" (DictEntry || ",")* ","? "}" ;

  DictEntry = key:VariableInit ":" value:VariableInit ;

  IndexExpression implements Expression = Expression "[" (IndexExpressionInner || ",")+ "]";

  interface IndexExpressionInner;

  SimpleIndex implements IndexExpressionInner = Expression;
  AllIndex implements IndexExpressionInner = ":";
  FromIndex implements IndexExpressionInner = from:Expression ":";
  UntilIndex implements IndexExpressionInner = ":" until:Expression;
  RangeIndex implements IndexExpressionInner = from:Expression (":"  step:Expression )? ":" until:Expression;

  // Walrus operator
  PyAssignmentExpression implements Expression = variable:Name operator:":=" right:Expression;

  // List/Set/Dict comprehension
  ListComprehensionExpression implements Expression = "[" Expression "for" ForControl "]";
  SetComprehensionExpression implements Expression = "{" Expression "for" ForControl "}";
  DictComprehensionExpression implements Expression = "{" Name ":" Expression "for" ForControl "}";
  GeneratorExpression implements Expression = Expression "for" ForControl;

  // function declaration statement
  interface FunctionDeclaration extends Function = Name ;

  SimpleFunctionDeclaration implements FunctionDeclaration, Statement = PyDecorator? "def" Name "(" FunctionParameters ")" ("->" returnType:TypeAnnotation)? ":"
                                                        StatementBlock ;
  FunctionParameters = (FunctionParameter  || ",")*;

  interface FunctionParameter extends Variable;

  TypeAnnotation = type:PyQualifiedName ("[" typeParams:(TypeParam || ",")* "]")?;
  TypeParam = PyQualifiedName;

  SimpleFunctionParameter   implements FunctionParameter = Name (":" TypeAnnotation)?;
  OptionalFunctionParameter implements FunctionParameter = Name (":" TypeAnnotation)? "=" Expression ;
  VarArgFunctionParameter   implements FunctionParameter = "*" Name (":" TypeAnnotation)?;

  @Override
  Arguments = "("
     ((paramName:Name "=")? Expression)?
     ( "," ((paramName:Name "=")? Expression) )*
     ","?
  ")";

  PyDecorator = "@" Name ("(" PyQualifiedName ")")? STATEMENT_END;

  // return statement
  ReturnStatement implements Statement = "return" (Expression || ",")* STATEMENT_END;

  RaiseStatement implements Statement = "raise" Expression? STATEMENT_END;

  // expression statement
  ExpressionStatement implements Statement = Expression STATEMENT_END;

  EmptyStatement implements Statement = STATEMENT_END;

  MatchStatement implements Statement = "match" Expression ":" MatchBlock;

  scope MatchBlock = BLOCK_START CaseStatement* BLOCK_END;
  CaseStatement = "case" (Expression || "|")+ ":" StatementBlock;

  /*===========================Literals======================================*/

  // string literals for python
   token StringPython
      = '\'' (StringCharactersPython)? '\''
      : {setText(getText().substring(1, getText().length() - 1));};

      fragment token StringCharactersPython
        = (StringCharacterPython)+;

      fragment token StringCharacterPython
        = ~ ('\''| '\\') | PythonEscapeSequence;

      fragment token PythonEscapeSequence
           = '\\' ('b' | 't' | 'n' | 'f' | 'r' | '\'' | '\\')
             | OctalEscape | UnicodeEscape;

   StringLiteralPython implements Literal, SignedLiteral =
        (type:key("u") | type:key("f") | type:key("r") | type:key("b") | type:key("R") | type:key("U")
        | type:key("F") | type:key("fr") | type:key("Fr") | type:key("fR") | type:key("FR") | type:key("rf")
        | type:key("rF") | type:key("Rf") | type:key("RF"))?
        (source:StringPython | source:String);

  // boolean literals for python
  BooleanLiteralPython implements Literal, SignedLiteral =
      source:["True" | "False"];

  /*======================Expressions===========================================*/

  // ternary-operator expression
  TernaryOperatorExpression implements Expression <200> = thenExpression:Expression ( "if" condition:Expression
                                       "else" elseExpression:Expression )+ ;

  //mathematical expression
  IntegerDivisionExpression implements Expression <165>, InfixExpression = left:Expression  operator:"//" right:Expression ;
  IntegerPowExpression implements Expression <195>, InfixExpression = left:Expression operator:"**" right:Expression ;

  //logical expressions
  AndExpression implements Expression <120>, InfixExpression = left:Expression operator:"and" right:Expression ;
  OrExpression implements Expression <117>, InfixExpression = left:Expression operator:"or" right:Expression ;
  NotExpression implements Expression <190> = "not" Expression ;
  IsExpression implements Expression <130>, InfixExpression = left:Expression operator:"is" right:Expression ;
  InExpression implements Expression <195>, InfixExpression = left:Expression operator:"in" right:Expression ;
  NotInExpression implements Expression <195>, InfixExpression = left:Expression operator:"not in" right:Expression ;

  // lambda expression
  scope LambdaExpression implements Expression = "lambda" FunctionParameters ":" Expression ;
  AppliedLambdaExpression implements Expression = "(" LambdaExpression ")" "(" Expression ")" ;


  /*===========================Classes======================================*/

  // class symbol
  interface scope symbol PythonClass = Name ;

  // class declaration statement
  ClassDeclaration implements PythonClass, Statement, ClassStatement = "class" Name ( "(" superClass:Name@PythonClass? ")" )? ":" ClassStatementBlock ;

  interface ClassStatement;

  ClassStatementBlock = BLOCK_START ClassStatementBlockBody BLOCK_END;
  ClassStatementBlockBody = ClassStatement+;

  // TODO: Why is there 2 function declarations?
  ClassFunctionDeclaration implements FunctionDeclaration, ClassStatement = PyDecorator? "def" Name "(" ClassFunctionParameters ")" ("->" returnType:TypeAnnotation)? ":" StatementBlock;

  ClassFunctionParameters = (FunctionParameter || ",")*;

  ClassAttributes implements ClassStatement = VariableDeclaration STATEMENT_END;
}
