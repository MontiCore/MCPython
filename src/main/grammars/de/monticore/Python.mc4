/* (c) https://github.com/MontiCore/monticore */
package de.monticore;

grammar Python extends de.monticore.expressions.CommonExpressions,
                        de.monticore.expressions.AssignmentExpressions,
                        de.monticore.literals.MCCommonLiterals,
                        de.monticore.symbols.BasicSymbols {

  token Indent = "    ";

  start PythonScript ;

  PythonScript = (Statement)*;

  EOL = ("\n" | "\r" | "<EOF>")+ ;

  EmptyStatement implements Statement = EOL ;

  interface Statement ;

  interface StatementBlock;

  //SingleLineStatementBlock implements StatementBlock = Statement ;
  MultiLineStatementBody implements StatementBlock = EOL? MultiLineStatementBlock {decreaseIndent()}? EOL?;
  MultiLineStatementBlock = Indent {isIndented()}? Indent* Statement {increaseIndent()}? ({isIndented()}? Indent+ Statement)*;

  token SingleLineComment = "#" (~('\n' | '\r' ))*  : ->skip {storeComment();};

  // variable declaration
  LocalVariableDeclarationStatement implements Statement = VariableDeclarator EOL;

  interface VariableDeclarator ;

  VariableDeclaration implements Variable, VariableDeclarator = Name "=" VariableInit ;

  interface VariableInit ;

  SimpleInit implements VariableInit = Expression ;
  ArrayInit implements VariableInit = "[" (VariableInit || ",")* "]" ;

  // if-else
  IfStatement implements Statement = "if" condition:Expression ":" thenStatement:StatementBlock ("else" ":" elseStatement:StatementBlock)? ;

  // for-loop
  scope ForStatement implements Statement = "for" ForControl ":" StatementBlock ;

  interface ForControl ;

  CommonForControl implements ForControl = forVariable:Name "in" Expression ;
  ArrayForControl implements ForControl = forVariable:Name "in" ArrayInit ;


  // while-loop
  WhileStatement implements Statement = "while" condition:Expression ":" EOL? StatementBlock;


  // function declaration

  FunctionDeclaration implements Statement, Function = "def" Name "(" FunctionDeclarationArguments ")" ":" EOL
                                                        StatementBlock? ReturnStatement?;

  interface FunctionDeclarationArguments ;

  SimpleFunctionDeclarationArguments implements FunctionDeclarationArguments = (Expression  || ",")* ;

  ReturnStatement implements Statement = "return" Expression? EOL ;

  // function call
  FunctionCall implements Statement = Name@Function "(" FunctionArguments ")" EOL?;
  FunctionArguments = (Expression || ",")* ;


  ExpressionStatement implements Statement = Expression EOL;

  // TODO: statement at file end,

  concept antlr {
    parserjava {
      public static int indentCounter = 0;

      public boolean isIndented() {
        if (indentCounter == 0 && !cmpTokenRegEx(1, "    |\t")) return true;
        return cmpTokenRegEx(indentCounter, "    |\t") && !cmpTokenRegEx(indentCounter + 1, "    |\t");
      }


      public boolean decreaseIndent() {
        if (isIndented() || (cmpTokenRegEx(1, "\n|\r") && cmpTokenRegEx(indentCounter, "    |\t")) || indentCounter <= 0) {
          return false;
        }
        indentCounter--;
        return true;
      }

      public boolean increaseIndent() {
        indentCounter++;
        return true;
      }

    }
  }
}
