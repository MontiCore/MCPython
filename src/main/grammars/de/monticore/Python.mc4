/* (c) https://github.com/MontiCore/monticore */
package de.monticore;

grammar Python extends de.monticore.expressions.CommonExpressions,
                        de.monticore.expressions.AssignmentExpressions,
                        de.monticore.literals.MCCommonLiterals,
                        de.monticore.symbols.BasicSymbols {

  PythonScript = (Statement | EOL)*;

  token Indent = "    ";

  EOL = ("\n" | "\r")+ ;


  interface Statement ;

  scope StatementBlock = EOL? StatementBlockBody {decreaseIndent()}? EOL? ;
  StatementBlockBody = Indent {isIndentedCorrectly()}? {increaseIndent()}? Indent* Statement ({isIndentedCorrectly()}? Indent+ Statement)*;

  token SingleLineComment = "#" (~('\n' | '\r' ))*  : ->skip {storeComment();};

  // imports
  ImportStatement implements Statement = ( "from" module:Name )? "import" Name EOL ;

  // variable declaration
  LocalVariableDeclarationStatement implements Statement = VariableDeclaration EOL ;

  VariableDeclaration implements Variable = Name "=" (VariableInit);

  interface VariableInit ;

  SimpleInit implements VariableInit = Expression ;
  ArrayInit implements VariableInit = "[" (VariableInit || ",")* "]" ;
  TupleInit implements VariableInit = "(" (VariableInit || ",")* ")" ;

    //trenary-operator
  TrenaryOperatorInit implements VariableInit =(Expression "if" condition:Expression "else" Expression)+;

  // if-else
  //IfStatement implements Statement = "if" condition:Expression ":" thenStatement:StatementBlock ("elif" elifCondition:Expression ":" elifStatement:StatementBlock)* ("else" ":" elseStatement:StatementBlock)?;









  // for-loop
  ForStatement implements Statement = "for" ForControl ":" StatementBlock ("else" ":" elseStatement:StatementBlock)? ;

  interface ForControl ;

  CommonForControl implements ForControl, Variable = Name "in" Expression ;
  ArrayForControl implements ForControl, Variable = Name "in" ArrayInit ;


  // while-loop
  WhileStatement implements Statement = "while" condition:Expression ":" EOL? StatementBlock ("else" ":" elseStatement:StatementBlock)?;


  // function declaration

  FunctionDeclaration implements Function, Statement = "def" Name "(" FunctionParameters ")" ":" EOL
                                                        StatementBlock;

  FunctionParameters = (FunctionParameter  || ",")*;

  interface FunctionParameter extends Variable;

  SimpleFunctionParameter implements FunctionParameter = Name;
  OptionalFunctionParameter implements FunctionParameter = Name "=" Expression ;

  ReturnStatement implements Statement = "return" Expression? EOL ;

  ExpressionStatement implements Statement = Expression EOL;

  // class declaration

  interface scope symbol Class = Name;

  ClassDeclaration implements Class, Statement = "class" Name ( "(" superClass:Name@Class? ")" )? ":" EOL ClassStatementBlock;

  interface ClassStatement;

  ClassStatementBlock = EOL? ClassStatementBlockBody {decreaseIndent()}? EOL? ;
  ClassStatementBlockBody = Indent {isIndentedCorrectly()}? {increaseIndent()}? Indent* ClassStatement ({isIndentedCorrectly()}? Indent+ ClassStatement)*;

  ClassFunction implements Function, ClassStatement = "def" Name "(" ClassFunctionParameters ")" ":" StatementBlock EOL?;

  ClassFunctionParameters = (selfParameter:Name "," (FunctionParameter  || ",")*);

  ClassAttributes implements ClassStatement = VariableDeclaration EOL;


  concept antlr {
    parserjava {
      public int indentCounter = 0;

      public boolean isIndentedCorrectly() {
        if (indentCounter == 0) {
          if (!cmpTokenRegEx(1, "    |\t")) {
            return true;
          } else {
            return false;
          }
        }
        return cmpTokenRegEx(indentCounter, "    |\t") && !cmpTokenRegEx(indentCounter + 1, "    |\t");
      }


      public boolean decreaseIndent() {
        if (isIndentedCorrectly()) {
          return false;
        }
        indentCounter--;
        return true;
      }

      public boolean increaseIndent() {
        indentCounter++;
        return true;
      }

    }
  }
}
