/* (c) https://github.com/MontiCore/monticore */
package de.monticore;

grammar Python extends de.monticore.expressions.CommonExpressions,
                        de.monticore.expressions.AssignmentExpressions,
                        de.monticore.literals.MCCommonLiterals,
                        de.monticore.symbols.BasicSymbols {

  // token Indent = "    ";

  start PythonScript ;

  PythonScript = (Statement)*;

  EOL = ("\n" | "\r" | "<EOF>")+ ;

  EmptyStatement implements Statement = EOL ;

  interface Statement ;

  interface StatementBlock;

  SingleLineStatementBlock implements StatementBlock = Statement ;
  MultiLineStatementBlock implements StatementBlock = EOL? "{" EOL? Statement* EOL? "}" EOL?;

  token SingleLineComment = "#" (~('\n' | '\r' ))*  : ->skip {storeComment();};

  // variable declaration
  LocalVariableDeclarationStatement implements Statement = LocalVariableDeclaration EOL;
  LocalVariableDeclaration = VariableDeclarator ;

  VariableDeclarator = Declarator "=" VariableInit ;

  interface Declarator = Name ;

  DeclaratorId implements Declarator = Name ;

  interface VariableInit ;

  SimpleInit implements VariableInit = Expression ;
  ArrayInit implements VariableInit = "[" (VariableInit || ",")* "]" ;

  // if-else
  IfStatement implements Statement = "if" condition:Expression ":" EOL? thenStatement:StatementBlock ("else" ":" EOL? elseStatement:StatementBlock)? ;

  // for-loop
  scope ForStatement implements Statement = "for" ForControl ":" EOL? StatementBlock ;

  interface ForControl ;

  CommonForControl implements ForControl = forVariable:Name "in" Expression ;
  ArrayForControl implements ForControl = forVariable:Name "in" ArrayInit ;


  // while-loop
  WhileStatement implements Statement = "while" condition:Expression ":" EOL? StatementBlock;


  // function declaration

  FunctionDeclaration implements Statement = "def" Name@Function "(" FunctionDeclarationArguments ")" ":" EOL
                                                        StatementBlock? ReturnStatement?;

  interface FunctionDeclarationArguments ;

  SimpleFunctionDeclarationArguments implements FunctionDeclarationArguments = (Expression  || ",")* ;

  ReturnStatement implements Statement = "return" Expression? EOL ;

  // function call
  FunctionCall implements Statement = Name@Function "(" FunctionArguments ")" EOL?;
  FunctionArguments = (Expression || ",")* ;


  ExpressionStatement implements Statement = Expression EOL;

  // TODO: statement at file end,
}
